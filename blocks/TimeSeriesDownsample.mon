/*
 * $Copyright (c) 2020-2023 Software AG, Darmstadt, Germany and/or Software AG USA Inc., Reston, VA, USA, and/or its subsidiaries and/or its affiliates and/or their licensors.$
 * Use, reproduction, transfer, publication or disclosure is prohibited except as specifically provided for in your License Agreement with Software AG
 */

package apamax.analyticsbuilder.blocks;

using apama.analyticsbuilder.BlockBase;
using apama.analyticsbuilder.Activation;
using apama.analyticsbuilder.Value;
using apama.analyticsbuilder.TimerParams;
using com.apama.exceptions.Exception;
using apama.analyticsbuilder.L10N;
using apama.analyticsbuilder.Promise;


event TimeSeriesDownsample_$State {
	float first;
	float last;
	float min;
	float max;

	float timestamp_first;
	float timestamp_last;
	float timestamp_min;
	float timestamp_max;


	action reset() {
		first := 0.0;
		last := 0.0;
		min := float.INFINITY;
		max := -float.INFINITY;
		timestamp_first := float.INFINITY;
		timestamp_last := float.INFINITY;
		timestamp_min := float.INFINITY;
		timestamp_max := float.INFINITY;
	}
	
	action update(float value, float activation_timestamp) {
		if(activation_timestamp < timestamp_first) {
			first := value;
			timestamp_first := activation_timestamp;
		}

		last := value;
		timestamp_last := activation_timestamp;

		if(value < min) {
			min := value;
			timestamp_min := activation_timestamp;
		}

		if(value > max) {
			max := value;
			timestamp_max := activation_timestamp;
		}
	}
}
/**
 * Time Series Downsample
 *
 * Downsamples discrete measurements.
 *
 * @$blockCategory Aggregates
 */
event TimeSeriesDownsample {

	BlockBase $base;


	/**
	 * Performs downsampling.
	 * @param $activation The current activation.
	 * @param $input_value The input value.
	 * @param $input_sample A new sample is provided.
	 */
	action $process(Activation $activation, float $input_value, boolean $input_sample, TimeSeriesDownsample_$State $blockState) {
		if $base.getInputCount("sample") = 0 or $input_sample {
			Value v :=new Value;
			dictionary<float,float> ts := new dictionary<float,float>;
			ts[$blockState.timestamp_first] := $blockState.first;
			ts[$blockState.timestamp_last] := $blockState.last;
			ts[$blockState.timestamp_min] := $blockState.min;
			ts[$blockState.timestamp_max] := $blockState.max;
			v.properties["measurements"] := ts;
			$setOutput_downSampledMeasurements($activation, v);
			$blockState.reset();
		} else {
			$blockState.update($input_value, $activation.timestamp);
		}
	}

	/**
	 * Downsampled Measurements
	 *
	 * Value containing a property <tt>measurements</tt> which is the measurements of the sampled period.
	 */
	action<Activation,Value> $setOutput_downSampledMeasurements;	

	constant string $OUTPUT_TYPE_downSampledMeasurements := "pulse";

	/**Defines type for input sample.*/
	constant string $INPUT_TYPE_sample := "pulse";

}
